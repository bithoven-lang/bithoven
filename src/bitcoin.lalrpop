use std::str::FromStr;
use super::ast::*;

grammar;

pub UTXO: UTXO = {
    "UTXO" "(" <stack:StackParamList> ")" <script: Script> => {
        UTXO {
            input_stack: stack,
            output_script: script,
        }
    }
}

pub Script: Vec<Statement> = {
    <statement:Statement*> => statement,
}

// Bitcoin script sig(witness) stack
pub StackParamList: Vec<StackParam> = {
    <first:StackParam> <more:MoreStackParams*> => {
        let mut stack = more;
        stack.insert(0, first);
        stack
    },
    => vec![], // epsilon production
};
pub StackParam: StackParam = {
    <i:Identifier> <c:Colon> <t:Type> => {
        StackParam {
            identifier: i,
            ty: t,
            value: None,
        }
    },
    <i:Identifier> <c:Colon> <t:Type> <a:Assign> <l:LiteralExpression> => {
        StackParam {
            identifier: i,
            ty: t,
            value: Some(l),
        }
    },
};
pub MoreStackParams: StackParam = <c:Comma> <s:StackParam> => s;

pub Statement: Statement = {
    <i:IfStatement> => <>,
    <b:BitcoinStatement> => <>,
}

// If statement follows semantics of bitcoin OP_(NOT)IF, OP_ELSE...
// reference1: https://bitcoin.stackexchange.com/questions/103844/inner-working-of-op-if-and-op-else
// reference2: https://bitcoin.stackexchange.com/questions/90868/does-bitcoin-script-support-nested-ifs-op-if-or-op-notif-inside-one-another
IfStatement: Statement = {
    "if" <c:Expression0> <b:BlockStatement> => {
        Statement::IfStatement {
            condition_expr: c,
            if_block: b,
            else_block: None,
        }
    },
    "if" <c:Expression0> <b1:BlockStatement> "else" <b2:BlockStatement> => {
        Statement::IfStatement {
            condition_expr: c,
            if_block: b1,
            else_block: Some(b2),
        }
    },
};
BlockStatement: Vec<Statement> = "{" <s:Script> "}" => s;

BitcoinStatement: Statement = {
    <AfterStatement> => Statement::BitcoinStatement(<>),
    <OlderStatement> => Statement::BitcoinStatement(<>),
    <VerifyStatement> => Statement::BitcoinStatement(<>),
}


// Below statements don't produce value(so, not expression), just perform an action.
// Bitcoin specific language sytax. OP_CLTV. inspired by miniscript syntax.
AfterStatement: BitcoinStatement = "after" <u:UnsignedInteger> <s:SemiColon> => {
    BitcoinStatement::AfterStatement(u as u32)
};
// Bitcoin specific language sytax. OP_CSV. inspired by miniscript syntax.
OlderStatement: BitcoinStatement = "older" <u:UnsignedInteger> <s:SemiColon> => {
    BitcoinStatement::OlderStatement(u as u32)
};
// Bitcoin specific language sytax. verify signature against public key
VerifyStatement: BitcoinStatement = "verify" <e:Expression0> <s:SemiColon> => {
    BitcoinStatement::VerifyStatement(e)
};

pub Expression0: Expression = {
    <ConditionExpr> => Expression::ConditionExpression(<>),
    <Expression1>,
};


pub Expression1: Expression = {
    <Identifier> => Expression::Variable(<>),
    <BooleanLiteral> => Expression::BooleanLiteral(<>),
    <StringLiteral> => Expression::StringLiteral(<>),
    <NumberLiteral> => Expression::NumberLiteral(<>),
    <BitcoinExpression>,
};

LiteralExpression: LiteralExpression = {
    <BooleanLiteral> => LiteralExpression::BooleanLiteral(<>),
    <StringLiteral> => LiteralExpression::StringLiteral(<>),
    <NumberLiteral> => LiteralExpression::NumberLiteral(<>),
}

// Support both OP_IF and OP_NOTIF
ConditionExpr: ConditionExpression = {
    <n:Not> "(" <c:CompareExpr> ")" => {
        ConditionExpression {
            negate: true,
            compare_expr: c
        }
    },
    "(" <c:CompareExpr> ")" => {
        ConditionExpression {
            negate: false,
            compare_expr: c
        }
    }, // to support js like syntax(while redundant)
    <c:CompareExpr> => {
        ConditionExpression {
            negate: false,
            compare_expr: c
        }
    },
}


// By separating CmpExpr, you can ensure that boolean operators (&&, ||) only operate on boolean values, 
// and arithmetic operators (+, -, *, /) only operate on numeric values.
CompareExpr: CompareExpression = {
    <l:Expression1> <c:BinaryCompareOp> <r:Expression1> => {
        CompareExpression {
            lhs: Box::new(l),
            op: c,
            rhs: Box::new(r),
        }
    }
}

MathExpr: Expression = {
    <l:Expression1> <c:BinaryMathOp> <r:Expression1> => {
        Expression::MathExpression {
            lhs: Box::new(l),
            op: c,
            rhs: Box::new(r),
        }
    }
};

BitcoinExpression: Expression = {
    <CheckSigExpr> => Expression::BitcoinExpression(<>),
    <Sha256Expr> => Expression::BitcoinExpression(<>),
    <Ripemd160Expr> => Expression::BitcoinExpression(<>),
}

// Below expressions produce value
// Bitcoin specific language sytax. verify signature against public key
CheckSigExpr: BitcoinExpression = "checksig" <a:BitcoinExprArgs> => BitcoinExpression::CheckSigExpr(a);
// Bitcoin specific language sytax. OP_SHA256. inspired by miniscript syntax.
Sha256Expr: BitcoinExpression = "sha256" <a:BitcoinExprArgs> => BitcoinExpression::Sha256Expr(a);
// Bitcoin specific language sytax. OP_RIPEMD160. inspired by miniscript syntax.
Ripemd160Expr: BitcoinExpression = "ripemd160" <a:BitcoinExprArgs> => BitcoinExpression::Ripemd160Expr(a);

// Either variable or string
pub BitcoinExprArgs: BitcoinExprArgs = {
    <Identifier> => BitcoinExprArgs::Variable(<>),
    <StringLiteral> => BitcoinExprArgs::StringLiteral(<>),
};

// Below are Token
// Terminal sequence in the context of lexer(tokenizer)

// Binary Ops(Minus can be both Unary and Binary)
BinaryMathOp: BinaryMathOp = {
    "+" => BinaryMathOp::Add,
    "-" => BinaryMathOp::Sub,
}

BinaryCompareOp: BinaryCompareOp = {
    "==" => BinaryCompareOp::Equal,
    "!=" => BinaryCompareOp::NotEqual,
    ">"  => BinaryCompareOp::Greater,
    ">=" => BinaryCompareOp::GreaterOrEqual,
    "<"  => BinaryCompareOp::Less,
    "<=" => BinaryCompareOp::LessOrEqual,
};
Assign: String           = "="  => "=".to_string();
And: String              = "&&" => "&&".to_string();
Or: String               = "||" => "||".to_string();
// UnaryOps
Not: String              = "!"  => "!".to_string();
Negation: String         = "~"  => "~".to_string();

// Puctuation
Comma: String = "," => ",".to_string();
Colon: String = ":" => ":".to_string();
SemiColon: String = ";" => ";".to_string();
OpenParen: String = "(" => "(".to_string();
CloseParen: String = ")" => ")".to_string();
OpenBrace: String = "{" => "{".to_string();
CloseBrace: String = "}" => "}".to_string();
OpenBracket: String = "[" => "[".to_string();
CloseBracket: String = "]" => "]".to_string();

// Below are terminals(more precisely, output terminals)
// Must be leaf node of AST

Type: Type = {
    "bool" => Type::Boolean,
    "string" => Type::String,
    "number" => Type::Number,
    "signature" => Type::Signature,
}

Identifier: Identifier = <v:r"[a-zA-Z_][a-zA-Z0-9_]*"> => Identifier(v.to_string());

StringLiteral: String = <l:r#""(\\\\|\\"|[^"\\])*""#> => l[1..l.len()-1].into();

BooleanLiteral: bool = {
    "true" => true,
    "false" => false,
}

NumberLiteral: i64 = {
     <p:UnsignedInteger> => p,
    // Unary minus at the Integer level(Precedence of fixed strings exists)
    // LALRPOP prefers to find the longest match first. 
    // After that, if there are two matches of equal length, it prefers the fixed string
    "-" <p:UnsignedInteger> => {
       // Number is 32 bit sign magnitude int, except when used as locktime.
       // To determine whether locktime or not is beyond context-free grammar.
       // Can be addressed when context analysis done
       if p > i32::MAX as i64 {
           panic!("overflow")
       }
       -p
    }
};

UnsignedInteger: i64 = <s:r"[0-9]+"> => {
    let integer = i64::from_str(s).unwrap();
    // Number is 32 bit sign magnitude int, except when used as locktime.
    // To determine whether locktime or not is beyond context-free grammar.
    // Can be addressed when context analysis done
    if integer <= i32::MIN as i64 || integer > u32::MAX as i64 {
        panic!("overflow")
    } else {
        integer
    }
};

pub Comment: () = { 
    r"\s*" => { }, // The default whitespace skipping is disabled if an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
}

// When there's Ambiguities between regular expressions,
// Use a match declaration, which lets you control the precedence between regular expressions.
// A match declaration lets you explicitly give the precedence between terminals
/*
match {
    r"[0-9]+"
} else {
    r"\w+",
    _
}
*/