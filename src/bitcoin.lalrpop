use std::str::FromStr;
use super::ast::*;

grammar;

pub UTXO: UTXO = {
    "UTXO" "(" <stack:StackParamList> ")" <script: Script> => {
        UTXO {
            input_stack: stack,
            output_script: script,
        }
    }
}

pub Script: Vec<Statement> = {
    <statement:Statement*> => statement,
}

// Bitcoin script sig(witness) stack
pub StackParamList: Vec<StackParam> = {
    <first:StackParam> <more:MoreStackParams*> => {
        let mut stack = more;
        stack.insert(0, first);
        stack
    },
    => vec![], // epsilon production
};
pub StackParam: StackParam = <i:Identifier> <c:Colon> <t:Type> => {
    StackParam {
        identifier: i,
        ty: t,
    }
};
pub MoreStackParams: StackParam = <c:Comma> <s:StackParam> => s;

pub Statement: Statement = {
    <d:VarDeclarationStatement> => <>,
    <e:ExprStatement> => Statement::ExprStatement(e),
    <i:IfStatement> => <>,
}

// No epsilon for Initalizer, don't neet to have Initializer rule
VarDeclarationStatement: Statement = "let" <i:Identifier> "=" <e:Expr> ";" => {
    Statement::VarDeclarationStatement {
        identifier: i,
        expr: e,
    }
};
// Need to have expression statement to avoid conflict of shift-reduce
ExprStatement: String = <Expr> ";" => (<>).into();
// If statement follows semantics of bitcoin OP_(NOT)IF, OP_ELSE...
// reference1: https://bitcoin.stackexchange.com/questions/103844/inner-working-of-op-if-and-op-else
// reference2: https://bitcoin.stackexchange.com/questions/90868/does-bitcoin-script-support-nested-ifs-op-if-or-op-notif-inside-one-another
IfStatement: Statement = {
    "if" <c:ConditionExpr> <b:BlockStatement> => {
        Statement::IfStatement {
            condition_expr: c,
            if_block: b,
            else_block: None,
        }
    },
    "if" <c:ConditionExpr> <b1:BlockStatement> "else" <b2:BlockStatement> => {
        Statement::IfStatement {
            condition_expr: c,
            if_block: b1,
            else_block: Some(b2),
        }
    },
};
BlockStatement: Vec<Statement> = "{" <s:Script> "}" => s;
/*
// Bitcoin specific language sytax. push data to stack. can be replaced [].push() later
PushDataStatement: String = "push" <Literal> ";" => (<>).into();
*/
// Bitcoin specific language sytax. verify signature against public key
VerifyExpr: String = "verify" <StringLiteral> => format!("(verify, {})", <>);
// Bitcoin specific language sytax. OP_CLTV. inspired by miniscript syntax.
AfterExpr: String = "after" <UnsignedInteger> => format!("(after, {})", <>);
// Bitcoin specific language sytax. OP_CSV. inspired by miniscript syntax.
OlderExpr: String = "older" <UnsignedInteger> => format!("(older, {})", <>);
// Bitcoin specific language sytax. OP_SHA256. inspired by miniscript syntax.
Sha256Expr: String = "sha256" <StringLiteral> => format!("(sha256, {})", <>);
// Bitcoin specific language sytax. OP_RIPEMD160. inspired by miniscript syntax.
Ripemd160Expr: String = "ripemd160" <StringLiteral> => format!("(ripemd160, {})", <>);


pub Expr: String = {
    <Literal>,
    <VerifyExpr>,
    <AfterExpr>,
    <OlderExpr>,
    <Sha256Expr>,
    <Ripemd160Expr>,
    <m:MathExpr> => m.to_string(),
};

// Support both OP_IF and OP_NOTIF
ConditionExpr: String = {
    <n:Not> "(" <c:CompareExpr> ")" => format!("(condition, ({}, {}))", <>),
    "(" <c:CompareExpr> ")" => format!("(condition, ({}))", <>), // to support js like syntax(while redundant)
    <c:CompareExpr> => format!("(condition, ({}))", <>),
}


// By separating CmpExpr, you can ensure that boolean operators (&&, ||) only operate on boolean values, 
// and arithmetic operators (+, -, *, /) only operate on numeric values.
CompareExpr: String = {
    <l:Expr> <c:Equal> <r:Expr> => format!("(compare, {}, ({}, {}))", c, l, r),
    <l:Expr> <c:NotEqual> <r:Expr> => format!("(compare, {}, ({}, {}))", c, l, r),
    <l:Expr> <c:Greater>  <r:Expr> => format!("(compare, {}, {}, {}))", c, l, r),
    <l:Expr> <c:GreaterOrEqual> <r:Expr> => format!("(compare, {}, ({}, {}))", c, l, r),
    <l:Expr> <c:Less>  <r:Expr> => format!("(compare, {}, ({}, {}))", c, l, r),
    <l:Expr> <c:LessOrEqual> <r:Expr> => format!("(compare, {}, ({}, {}))", c, l, r),
}

MathExpr: i64 = {
    <l:MathExpr> <p:Plus> <r:MathTerm> => l + r,
    <l:MathExpr> <m:Minus> <r:MathTerm> => l - r,
    <t:MathTerm> => t,  // Important: This allows a Term to be an Expr
};

// Might be redundant as not support multiplication/division
MathTerm: i64 = {
    <f:MathFactor> => f, // Important: This allows a Factor to be a Term
};

MathFactor: i64 = {
     <p:MathPrimary> => p,
    // Unary minus at the Integer level(Precedence of fixed strings exists)
    // LALRPOP prefers to find the longest match first. 
    // After that, if there are two matches of equal length, it prefers the fixed string
    <m:Minus> <p:MathPrimary> => {
       // Number is 32 bit sign magnitude int, except when used as locktime.
       // To determine whether locktime or not is beyond context-free grammar.
       // Can be addressed when context analysis done
       if p > i32::MAX as i64 {
           panic!("overflow")
       }
       -p
    }
};

MathPrimary: i64 = {
    <i:UnsignedInteger> => i,
    "(" <e:MathExpr> ")" => e,
};

// Below are Token
// Terminal sequence in the context of lexer(tokenizer)

// Binary Ops(Minus can be both Unary and Binary)
Plus: String             = "+"  => "+".to_string();
Minus: String            = "-"  => "-".to_string();
Equal: String            = "==" => "==".to_string();
NotEqual: String         = "!=" => "!=".to_string();
Greater: String          = ">"  => ">".to_string();
GreaterOrEqual: String   = ">=" => ">=".to_string();
Less: String             = "<"  => "<".to_string();
LessOrEqual: String      = "<=" => "<=".to_string();
Assign: String           = "="  => "=".to_string();
And: String              = "&&" => "&&".to_string();
Or: String               = "||" => "||".to_string();
// UnaryOps
Not: String              = "!"  => "!".to_string();
Negation: String         = "~"  => "~".to_string();

// Puctuation
Comma: String = "," => ",".to_string();
Colon: String = ":" => ":".to_string();
SemiColon: String = ";" => ";".to_string();
OpenParen: String = "(" => "(".to_string();
CloseParen: String = ")" => ")".to_string();
OpenBrace: String = "{" => "{".to_string();
CloseBrace: String = "}" => "}".to_string();
OpenBracket: String = "[" => "[".to_string();
CloseBracket: String = "]" => "]".to_string();

// Below are terminals(more precisely, output terminals)
// Must be leaf node of AST

Type: Type = {
    "bool" => Type::Boolean,
    "string" => Type::String,
    "number" => Type::Number,
    "signature" => Type::Signature,
}

Identifier: String = <v:r"[a-zA-Z_][a-zA-Z0-9_]*"> => v.into();

UnsignedInteger: i64 = <s:r"[0-9]+"> => {
    let integer = i64::from_str(s).unwrap();
    // Number is 32 bit sign magnitude int, except when used as locktime.
    // To determine whether locktime or not is beyond context-free grammar.
    // Can be addressed when context analysis done
    if integer <= i32::MIN as i64 || integer > u32::MAX as i64 {
        panic!("overflow")
    } else {
        integer
    }
};

Literal: String = {
    <b:BooleanLiteral> => b,
    <s:StringLiteral> => s,
}

StringLiteral: String = <l:r#""(\\\\|\\"|[^"\\])*""#> => l[1..l.len()-1].into();

BooleanLiteral: String = {
    "true" => "true".to_string(),
    "false" => "false".to_string(),
}

/*
// Bitcoin integer is sign-magnitude integer.
// Don't know need to differentiate yet.
UnaryOp: String = {
    "-" => "-".to_string(),
    "~" => "~".to_string(),
    "!" => "!".to_string(),
}

BinaryAddOp: String = {
    "+"  => "+".to_string(),
    "-"  => "-".to_string(),
}

BinaryRelOp: String = {
    "==" => "!=".to_string(),
    "!=" => "!=".to_string(),
    ">"  => ">".to_string(),
    ">=" => ">=".to_string(),
    "<"  => "<".to_string(),
    "<=" => "<=".to_string(),
    "&&" => "&&".to_string(),
    "||" => "||".to_string(),
}
*/
pub Comment: () = { 
    r"\s*" => { }, // The default whitespace skipping is disabled if an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
}

// When there's Ambiguities between regular expressions,
// Use a match declaration, which lets you control the precedence between regular expressions.
// A match declaration lets you explicitly give the precedence between terminals
/*
match {
    r"[0-9]+"
} else {
    r"\w+",
    _
}
*/